World1:  db  World1MapBlock |  dw World1Map |  db World1ObjectLayerMapBlock |  dw World1ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World2:  db  World2MapBlock |  dw World2Map |  db World2ObjectLayerMapBlock |  dw World2ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World3:  db  World3MapBlock |  dw World3Map |  db World3ObjectLayerMapBlock |  dw World3ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World4:  db  World4MapBlock |  dw World4Map |  db World4ObjectLayerMapBlock |  dw World4ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World5:  db  World5MapBlock |  dw World5Map |  db World5ObjectLayerMapBlock |  dw World5ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World6:  db  World6MapBlock |  dw World6Map |  db World6ObjectLayerMapBlock |  dw World6ObjectLayerMap |  db World1TilesBlock         |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World7:  db  World7MapBlock |  dw World7Map |  db World7ObjectLayerMapBlock |  dw World7ObjectLayerMap |  db TilesSdSnatcherBlock     |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World8:  db  World8MapBlock |  dw World8Map |  db World8ObjectLayerMapBlock |  dw World8ObjectLayerMap |  db TilesSolidSnakeBlock     |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4
World9:  db  World9MapBlock |  dw World9Map |  db World9ObjectLayerMapBlock |  dw World9ObjectLayerMap |  db TilesGentleBlock         |   incbin"..\grapx\tilesheets\PaletteGentle.pl",0,4
World10: db  World10MapBlock | dw World10Map | db World10ObjectLayerMapBlock | dw World10ObjectLayerMap | db TilesGentleDesertBlock   |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4
World11: db  World11MapBlock | dw World11Map | db World11ObjectLayerMapBlock | dw World11ObjectLayerMap | db TilesGentleAutumnBlock   |   incbin"..\grapx\tilesheets\PaletteGentleAutumn.pl",0,4
World12: db  World12MapBlock | dw World12Map | db World12ObjectLayerMapBlock | dw World12ObjectLayerMap | db TilesGentleWinterBlock   |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4
World13: db  World13MapBlock | dw World13Map | db World13ObjectLayerMapBlock | dw World13ObjectLayerMap | db TilesGentleJungleBlock   |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4
World14: db  World14MapBlock | dw World14Map | db World14ObjectLayerMapBlock | dw World14ObjectLayerMap | db TilesGentleCaveBlock     |   incbin"..\grapx\tilesheets\PaletteGentleCave.pl",0,4


GentleMap01: db  GentleMap01MapBlock | dw GentleMap01Map | db GentleMap01ObjectLayerMapBlock | dw GentleMap01ObjectLayerMap | db TilesGentleBlock     |   incbin"..\grapx\tilesheets\PaletteGentle.pl",0,4

GentleDesertMap01: db  GentleDesertMap01MapBlock | dw GentleDesertMap01Map | db GentleDesertMap01ObjectLayerMapBlock | dw GentleDesertMap01ObjectLayerMap | db TilesGentleDesertBlock     |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4
GentleDesertMap02: db  GentleDesertMap02MapBlock | dw GentleDesertMap02Map | db GentleDesertMap02ObjectLayerMapBlock | dw GentleDesertMap02ObjectLayerMap | db TilesGentleDesertBlock     |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4
GentleDesertMap03: db  GentleDesertMap03MapBlock | dw GentleDesertMap03Map | db GentleDesertMap03ObjectLayerMapBlock | dw GentleDesertMap03ObjectLayerMap | db TilesGentleDesertBlock     |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4
GentleDesertMap04: db  GentleDesertMap04MapBlock | dw GentleDesertMap04Map | db GentleDesertMap04ObjectLayerMapBlock | dw GentleDesertMap04ObjectLayerMap | db TilesGentleDesertBlock     |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4
GentleDesertMap05: db  GentleDesertMap05MapBlock | dw GentleDesertMap05Map | db GentleDesertMap05ObjectLayerMapBlock | dw GentleDesertMap05ObjectLayerMap | db TilesGentleDesertBlock     |   incbin"..\grapx\tilesheets\PaletteGentleDesert.pl",0,4

GentleWinterMap01: db  GentleWinterMap01MapBlock | dw GentleWinterMap01Map | db GentleWinterMap01ObjectLayerMapBlock | dw GentleWinterMap01ObjectLayerMap | db TilesGentleWinterBlock     |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4
GentleWinterMap02: db  GentleWinterMap02MapBlock | dw GentleWinterMap02Map | db GentleWinterMap02ObjectLayerMapBlock | dw GentleWinterMap02ObjectLayerMap | db TilesGentleWinterBlock     |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4
GentleWinterMap03: db  GentleWinterMap03MapBlock | dw GentleWinterMap03Map | db GentleWinterMap03ObjectLayerMapBlock | dw GentleWinterMap03ObjectLayerMap | db TilesGentleWinterBlock     |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4
GentleWinterMap04: db  GentleWinterMap04MapBlock | dw GentleWinterMap04Map | db GentleWinterMap04ObjectLayerMapBlock | dw GentleWinterMap04ObjectLayerMap | db TilesGentleWinterBlock     |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4
GentleWinterMap05: db  GentleWinterMap05MapBlock | dw GentleWinterMap05Map | db GentleWinterMap05ObjectLayerMapBlock | dw GentleWinterMap05ObjectLayerMap | db TilesGentleWinterBlock     |   incbin"..\grapx\tilesheets\PaletteGentleWinter.pl",0,4

GentleJungleMap01: db  GentleJungleMap01MapBlock | dw GentleJungleMap01Map | db GentleJungleMap01ObjectLayerMapBlock | dw GentleJungleMap01ObjectLayerMap | db TilesGentleJungleBlock     |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4
GentleJungleMap02: db  GentleJungleMap02MapBlock | dw GentleJungleMap02Map | db GentleJungleMap02ObjectLayerMapBlock | dw GentleJungleMap02ObjectLayerMap | db TilesGentleJungleBlock     |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4
GentleJungleMap03: db  GentleJungleMap03MapBlock | dw GentleJungleMap03Map | db GentleJungleMap03ObjectLayerMapBlock | dw GentleJungleMap03ObjectLayerMap | db TilesGentleJungleBlock     |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4
GentleJungleMap04: db  GentleJungleMap04MapBlock | dw GentleJungleMap04Map | db GentleJungleMap04ObjectLayerMapBlock | dw GentleJungleMap04ObjectLayerMap | db TilesGentleJungleBlock     |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4
GentleJungleMap05: db  GentleJungleMap05MapBlock | dw GentleJungleMap05Map | db GentleJungleMap05ObjectLayerMapBlock | dw GentleJungleMap05ObjectLayerMap | db TilesGentleJungleBlock     |   incbin"..\grapx\tilesheets\PaletteGentleJungle.pl",0,4


;tiles:
;000-015 no obstacle, but hero walks behind it, but they are not see through
;016-023 obstacle
;024-148 walkable terrain pieces (no obstacle)

;non_see_throughpieces:      equ 16    ;tiles 0-15: background pieces a hero can stand behind, but they are not see through
;amountoftransparantpieces:  equ 64    ;tiles 16-63: see-through background pieces hero can stand behind
;UnwalkableTerrainPieces:    equ 149   ;tiles 149 and up are unwalkable terrain

;mirrortransparentpieces:                ;(piece number,) ymirror, xmirror
;  ds	16*2 ;the first 16 background pieces a hero can stand behind, but they are not see through
;  db    064,000, 064,016, 064,032, 064,048, 064,064, 064,080, 064,096, 064,112, 048,128, 080,144, 048,160, 080,128, 064,176, 064,192, 000,000, 000,000
;  db    064,000, 064,016, 064,032, 064,048, 064,064, 064,080, 064,096, 064,112, 064,128, 064,144, 064,160, 080,160, 080,176, 080,192, 048,224, 048,240
;  db    080,000, 080,016, 080,032, 080,048, 080,064, 080,080, 080,096, 080,112, 000,000, 000,000, 000,000, 000,000, 080,192, 080,208, 000,000, 000,000
  
  
  
  
  
;hud is placed in page 0 and will be copied to page 1 after this. So create minimap in page 0
;minimap is 48x48. Worldmap is 128x128
;48:128 = 3:8.. so read tile/pixel 1, 4, 7
CreateMiniMap:                          ;using worldtiles from page 3 and worldmap, we can generate minimap
  ld    a,(slot.page1rom)             ;all RAM except page 1
  out   ($a8),a      

  ld		a,1                             ;set worldmap in bank 1 at $8000
  out   ($fe),a          	              ;$ff = page 0 ($c000-$ffff) | $fe = page 1 ($8000-$bfff) | $fd = page 2 ($4000-$7fff) | $fc = page 3 ($0000-$3fff) 

  ld    a,YMiniMap
	ld		(CopyTilePiece+dy),a

  ld    hl,$8000                        ;worldmap mapdata
  ld    c,16                            ;16*3 pixel y-axis
  ld    b,16                            ;16*3 pixel x-axis

  .loop:
  push  bc
  call  .Copy1Row                       ;row 1 (y-axis)
  pop   bc
  ld    de,256
  add   hl,de
  push  bc
  call  .Copy1Row                       ;row 4 (y-axis)
  pop   bc
  ld    de,256
  add   hl,de
  push  bc
  call  .Copy1Row                       ;row 7 (y-axis)
  pop   bc
  ld    de,128
  add   hl,de
  dec   c
  jr    nz,.loop
  ret

  .Copy1Row:
  call  .GoCopyTilePiece                ;pixel 1 (x-axis)
  inc   hl
  inc   hl
  inc   hl
  call  .GoCopyTilePiece                ;pixel 4 (x-axis)
  inc   hl
  inc   hl
  inc   hl
  call  .GoCopyTilePiece                ;pixel 7 (x-axis)
  inc   hl
  inc   hl
  djnz  .Copy1Row
  
  ld    a,XMiniMap
	ld		(CopyTilePiece+dx),a
	ld		a,(CopyTilePiece+dy)
  inc   a
	ld		(CopyTilePiece+dy),a
  ret

  .GoCopyTilePiece:
  push  bc
  push  hl

  ld    a,(hl)                          ;tilenr

	;set sx
	ld		e,a                             ;store tilenr
	add		a,a				                      ;*2
	add		a,a				                      ;*4
	add		a,a				                      ;*8
	add		a,a				                      ;*16
	ld		(CopyTilePiece+sx),a
	;/set sx

	;set sy
	ld		a,e
	ld		d,-1
.setsy:
	sub		a,16
	inc		d
	jp		nc,.setsy
	ld		a,d
	add		a,a				                      ;*2
	add		a,a				                      ;*4
	add		a,a				                      ;*8
	add		a,a				                      ;*16
	ld		(CopyTilePiece+sy),a
	;/set sy
  
  ld    hl,CopyTilePiece
  call  DoCopy

  ld    a,(CopyTilePiece+dx)
  inc   a
  ld    (CopyTilePiece+dx),a

  pop   hl
  pop   bc
  ret

XMiniMap: equ 203
YMiniMap: equ 005

SetCastlesInMiniMap:
  ld    ix,Castle1
  call  .SetCastle
  ld    ix,Castle2
  call  .SetCastle
  ld    ix,Castle3
  call  .SetCastle
  ld    ix,Castle4
  call  .SetCastle
  ret

  .SetCastle:
;minimap is 48x48. Worldmap is 128x128
;48:128 = 3:8.. so read tile/pixel 1, 4, 7  
  ld    a,(ix+CastleX)
  cp    255
  ret   z
  ld    h,0
  ld    l,a
  ld    de,3                            ;first multiply by 3
  call  MultiplyHlWithDE                ;Out: HL = result
  push  hl
  pop   bc
  ld    de,8                            ;then divide by 8
  call  DivideBCbyDE                    ;In: BC/DE. Out: BC = result, HL = rest
  push  bc
  
  ld    l,(ix+CastleY)
  ld    h,0
  ld    de,3                            ;first multiply by 3
  call  MultiplyHlWithDE                ;Out: HL = result
  push  hl
  pop   bc
  ld    de,8                            ;then divide by 8
  call  DivideBCbyDE                    ;In: BC/DE. Out: BC = result, HL = rest

  pop   de
  ld    a,c
  add   YMiniMap-2
  ld    d,a

  ld    a,e
  add   XMiniMap-1
  ld    e,a
  
  ld    hl,$4000 + (071*128) + (212/2) - 128
;  ld    de,256*025 + 220                ;(dy*256 + dx)
  ld    bc,$0000 + (004*256) + (004/2)
  ld    a,HudNewBlock                   ;block to copy graphics from  
  exx
  ex    af,af'
  ld    hl,CopyTransparantImageEXX
  jp    EnterSpecificRoutineInCastleOverviewCode

ConvertMonstersObjectLayer:
 ; ld    a,(slot.page1rom)             ;all RAM except page 1
 ; out   ($a8),a      

 ; ld		a,2                           ;set worldmap object layer in bank 2 at $8000
 ; out   ($fe),a          	            ;$ff = page 0 ($c000-$ffff) | $fe = page 1 ($8000-$bfff) | $fd = page 2 ($4000-$7fff) | $fc = page 3 ($0000-$3fff) 
  
  ld    hl,$8000-1

  .loopAndXorA:
  xor   a
  .loop:
  inc   hl
  bit   6,h                           ;check if hl=$c000 (end of object layer in ram)
  ret   nz

  or    (hl)
  jp    z,.loop
  
  .Check:
  cp    7
  jr    nc,.loopAndXorA

  dec   a
  jp    z,.Level1Monster
  dec   a
  jr    z,.Level2Monster
  dec   a
  jr    z,.Level3Monster
  dec   a
  jr    z,.Level4Monster
  dec   a
  jr    z,.Level5Monster

  .Level6Monster:
  ld    de,(MonsterLevel6Pointer)
  ld    a,(de)
  cp    255
  jr    z,.Level5Monster
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel6Pointer
  ld    de,ListOfUnlockedMonstersLevel6
  .SetMonsterLevel6Pointer:
  ld    (MonsterLevel6Pointer),de
  jp    .loopAndXorA

  .Level5Monster:
  ld    de,(MonsterLevel5Pointer)
  ld    a,(de)
  cp    255
  jr    z,.Level4Monster
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel5Pointer
  ld    de,ListOfUnlockedMonstersLevel5
  .SetMonsterLevel5Pointer:
  ld    (MonsterLevel5Pointer),de
  jp    .loopAndXorA

  .Level4Monster:
  ld    de,(MonsterLevel4Pointer)
  ld    a,(de)
  cp    255
  jr    z,.Level3Monster
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel4Pointer
  ld    de,ListOfUnlockedMonstersLevel4
  .SetMonsterLevel4Pointer:
  ld    (MonsterLevel4Pointer),de
  jp    .loopAndXorA

  .Level3Monster:
  ld    de,(MonsterLevel3Pointer)
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel3Pointer
  ld    de,ListOfUnlockedMonstersLevel3
  .SetMonsterLevel3Pointer:
  ld    (MonsterLevel3Pointer),de
  jp    .loopAndXorA

  .Level2Monster:
  ld    de,(MonsterLevel2Pointer)
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel2Pointer
  ld    de,ListOfUnlockedMonstersLevel2
  .SetMonsterLevel2Pointer:
  ld    (MonsterLevel2Pointer),de
  jp    .loopAndXorA
  
  .Level1Monster:
  ld    de,(MonsterLevel1Pointer)
  call  .ConvertLevelToMonster
  jr    nz,.SetMonsterLevel1Pointer
  ld    de,ListOfUnlockedMonstersLevel1
  .SetMonsterLevel1Pointer:
  ld    (MonsterLevel1Pointer),de
  jp    .loopAndXorA

  .ConvertLevelToMonster:
  ld    a,(de)
  ld    (hl),a
  cp    160                           ;monster nr 160 and above are 1 tile in size
  jr    nc,.EndCheck1Or2TilesInSize
  ld    bc,-128
  add   hl,bc
  add   a,64                          ;bottom part of monster is 64 tiles higher
  ld    (hl),a
  sbc   hl,bc
    
  .EndCheck1Or2TilesInSize:
  inc   de
  ld    a,(de)
  or    a
  ret

CastleTileNr: equ 254
FindAndSetCastles:                      ;castles on the map have to be assigned to their players, and coordinates have to be set
 ; ld    a,(slot.page1rom)             ;all RAM except page 1
 ; out   ($a8),a      

 ; ld		a,2                           ;set worldmap object layer in bank 2 at $8000
 ; out   ($fe),a          	            ;$ff = page 0 ($c000-$ffff) | $fe = page 1 ($8000-$bfff) | $fd = page 2 ($4000-$7fff) | $fc = page 3 ($0000-$3fff) 
  
  ld    hl,$8000-1
  ld    ix,Castle1                      ;first castle in castle list

  .SetCastleTileNrAndGoloop:
  ld    a,CastleTileNr
  .loop:
  inc   hl
  bit   6,h                             ;hl=$c000 ? (this happens when bit 6 of h=1)
  ret   nz
  cp    (hl)
  jr    nz,.loop

  ld    a,l                             ;x value castle
  and   127
  dec   a
  ld    (ix+CastleX),a

  push  hl

  push  hl
  pop   bc

  inc   hl                              ;player that owns this castle
  ld    a,(hl)
  sub   a,245
  ld    (ix+CastlePlayer),a
  ld    (hl),0                          ;remove number from object map

  ld    de,128
  call  DivideBCbyDE                    ;In: BC/DE. mOut: BC = result, HL = rest
 
  ld    a,c                             ;y value castle
  inc   a
  ld    (ix+CastleY),a  

  pop   hl
  ld    de,LenghtCastleTable
  add   ix,de  
  jp    .SetCastleTileNrAndGoloop

PlaceHeroesInCastles:                   ;Place hero nr#1 in their castle
  ld    ix,Castle1
  call  .PlaceHero
  ld    ix,Castle2
  call  .PlaceHero
  ld    ix,Castle3
  call  .PlaceHero
  ld    ix,Castle4
  call  .PlaceHero
  ret

  .PlaceHero:
  ld    a,(ix+CastlePlayer)
  ld    iy,pl1hero1y
  cp    1
  jr    z,.SetHeroInCastle
  ld    iy,pl2hero1y
  cp    2
  jr    z,.SetHeroInCastle
  ld    iy,pl3hero1y
  cp    3
  jr    z,.SetHeroInCastle
  ld    iy,pl4hero1y
  cp    4
  jr    z,.SetHeroInCastle
  .SetHeroInCastle:
  ld    a,(ix+CastleY)
  dec   a
  ld    (iy+HeroY),a
  ld    a,(ix+CastleX)
  add   a,2
  ld    (iy+HeroX),a
  ld    (iy+HeroStatus),2               ;1=active on map, 2=visiting castle,254=defending in castle, 255=inactive
  ret

; Check/Init Mouse
CHMOUS:	
  CALL	.MOUSIN
	LD	A,$10	; first port 1
	LD	(MSEPRT),A
	CALL	.CHMS.0
	JP	NC,.MOUSIN
	LD	A,$60	; port 2
	LD	(MSEPRT),A
	CALL	.CHMS.0
	JP	NC,.MOUSIN
	
	XOR	A	; not found
	LD	(MOUSID),A
;	LD	(MSEPRT),A
;	SCF	
	RET	
	
  ; Install Mouse
.MOUSIN:	LD	A,255
	LD	(MOUSID),A
	LD	HL,DLY_M2	; delay routs
;	LD	A,(ComputerID)              ;3=turbo r, 2=msx2+, 1=msx2, 0=msx1
;	CP	3
;	JP	C,.MSIN.0
;	CALL	$0183
;	AND	A
;	JP	Z,.MSIN.0
;	LD	HL,DLY_TR
;.MSIN.0:	
;  LD	(DLYCAL+1),HL
;	AND	A
	EI	
	RET	
	
.CHMS.0:	LD	B,40	; check 40 times
.CHMS.1:	PUSH	BC
	CALL	.RDPADL
	LD	A,H	; Y-off
	CP	1	; Y-off
	JP	NZ,.CHMS.2
	LD	A,L	; X-off
	CP	1
	JP	NZ,.CHMS.2
	POP	BC
	DJNZ	.CHMS.1
	SCF		; Cy:1 (not found)
	RET	
.CHMS.2:	POP	BC	; found
	AND	A	; Cy:0
	RET	


; Read padle
; Out: (MSEOFS), mouse offsets (Y,X)
;       HL, mouse offsets (XXYY)
.RDPADL:	PUSH	BC
	PUSH	DE
	
	LD	DE,(MSEPRT)
	LD	A,15	; Read PSG r15 port B
	CALL	RD_PSG
	AND	%10001111	; interface 1
	OR	E	; mouse NR
	LD	E,A
	
; X offset
	LD	B,40	; delay Z80
	LD	C,20	; delay R800
	CALL	.RPDL.2
	CALL	.RPDL.0
	LD	H,A
	
; Y offset
	CALL	.RPDL.1
	CALL	.RPDL.0
	LD	L,A
	
	EI	
	POP	DE
	POP	BC
	RET	
	
.RPDL.0:	RLCA	
	RLCA	
	RLCA	
	RLCA	
	LD	D,A
	CALL	.RPDL.1
	OR	D
	NEG	
	RET	
	
.RPDL.1:	LD	B,7
	LD	C,6
.RPDL.2:	LD	A,15
	CALL	WR_PSG
	LD	A,(MSEPRT)
	AND	$30
	XOR	E
	LD	E,A
.DLYCAL:	CALL	DLY_M2
	LD	A,14
	CALL	RD_PSG
	AND	$0F
	RET	